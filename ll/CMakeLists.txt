###########
# Library #
###########

set(LIBRARY_NAME "${PROJECT_NAME}")

set(Header_Files "ll.h")
if (MSVC_VERSION STRLESS "1600" # MSVC 2010
  OR CMAKE_C_COMPILER_ID STREQUAL "OpenWatcom"
  OR CMAKE_C_STANDARD EQUAL 90 AND CMAKE_C_STANDARD_REQUIRED)
    list(APPEND Header_Files "ll_stdbool.h")
endif (MSVC_VERSION STRLESS "1600" # MSVC 2010
        OR CMAKE_C_COMPILER_ID STREQUAL "OpenWatcom"
        OR CMAKE_C_STANDARD EQUAL 90 AND CMAKE_C_STANDARD_REQUIRED)
source_group("Header Files" FILES "${Header_Files}")

set(Source_Files "ll.c")
source_group("Source Files" FILES "${Source_Files}")

add_library("${LIBRARY_NAME}" "${Header_Files}" "${Source_Files}")

include(GNUInstallDirs)
target_include_directories(
        "${LIBRARY_NAME}"
        PUBLIC
        "$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>"
        "$<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>"
        "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>"
)

if (NOT DEFINED HAVE_ASPRINTF)
    find_package(c89stringutils CONFIG REQUIRED)
    target_link_libraries("${LIBRARY_NAME}" PRIVATE c89stringutils c89stringutils_compiler_flags)
endif (NOT DEFINED HAVE_ASPRINTF)

if (CMAKE_VERSION VERSION_GREATER_EQUAL "3.11" AND NOT DEFINED VCPKG_TOOLCHAIN AND NOT VCPKG_TOOLCHAIN)
    include(FetchContent)
#    FetchContent_Declare(
#            c_str_span
#            URL      https://api.github.com/repos/SamuelMarks/c-str-span/zipball/7575bfdd213a2f90e571e4d03452b57bdb4648db
#            URL_HASH SHA256=1d284234e727315cf6dda79a2b53e243fa5c11f30db2feb97c44e0e4117b0c64
#    )
    FetchContent_Declare(
            c_str_span
            GIT_REPOSITORY https://github.com/SamuelMarks/c-str-span.git
            GIT_TAG        7575bfdd213a2f90e571e4d03452b57bdb4648db # release-1.10.0
    )
    FetchContent_MakeAvailable(c_str_span)
    find_package(c_str_span REQUIRED)
else ()
    find_package(c_str_span CONFIG REQUIRED)
endif ()


target_link_libraries(
        "${LIBRARY_NAME}"
        PUBLIC
        c_str_span c_str_span_compiler_flags
)

target_link_libraries(
        "${LIBRARY_NAME}"
        PRIVATE
        "${PROJECT_NAME}_compiler_flags"
)

set_target_properties("${LIBRARY_NAME}" PROPERTIES LINKER_LANGUAGE C)

include(GenerateExportHeader)
set(_export_file "${CMAKE_CURRENT_BINARY_DIR}/${LIBRARY_NAME}_export.h")
generate_export_header("${LIBRARY_NAME}" EXPORT_FILE_NAME "${_export_file}")

#################
# Install rules #
#################

include(GNUInstallDirs)

# setup the version numbering
set_property(TARGET "${LIBRARY_NAME}" PROPERTY VERSION "${${PROJECT_NAME}_VERSION}")
set_property(TARGET "${LIBRARY_NAME}" PROPERTY SOVERSION "${${PROJECT_NAME}_VERSION_MAJOR}")

set(installable_libs # "${EXEC_NAME}"
        "${LIBRARY_NAME}")
if (TARGET "${DEPENDANT_LIBRARY}")
    list(APPEND installable_libs "${DEPENDANT_LIBRARY}")
endif ()
if (CMAKE_VERSION VERSION_GREATER_EQUAL "3.13")
    list(APPEND installable_libs "${PROJECT_NAME}_compiler_flags")
endif ()
install(TARGETS ${installable_libs}
        EXPORT "${LIBRARY_NAME}Targets"
        ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}"
        LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}"
        RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}")
install(EXPORT "${LIBRARY_NAME}Targets" DESTINATION "${CMAKE_INSTALL_DATADIR}/${LIBRARY_NAME}")

install(FILES ${Header_Files} "${_export_file}"
        DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}")
